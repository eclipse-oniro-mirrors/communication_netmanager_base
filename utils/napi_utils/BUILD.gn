# Copyright (c) 2022-2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/communication/netmanager_base/netmanager_base_config.gni")

config("napi_utils_config") {
  visibility = [ ":napi_utils" ]
  include_dirs = [
    "$INNERKITS_ROOT/include",
    "$NETMANAGER_BASE_ROOT/utils/common_utils/include",
  ]
}

config("napi_utils_public_config") {
  include_dirs = [
    "include",
    "$INNERKITS_ROOT/include",
    "$NETMANAGER_BASE_ROOT/utils/errorcode_utils/include",
    "$NETMANAGER_BASE_ROOT/utils/common_utils/include",
  ]

  cflags = common_cflags

  cflags_cc = common_cflags

  if (product_name != "qemu-arm-linux-min") {
    defines = [ "ENABLE_EMULATOR" ]
  }
}

ohos_shared_library("napi_utils") {
  sources = [
    "src/base_context.cpp",
    "src/event_listener.cpp",
    "src/event_manager.cpp",
    "src/module_template.cpp",
    "src/napi_utils.cpp",
    "src/hi_app_event_report.cpp",
  ]

  branch_protector_ret = "pac_ret"

  configs = [ ":napi_utils_config" ]

  public_configs = [ ":napi_utils_public_config" ]

  deps = [ "$NETMANAGER_BASE_ROOT/utils:net_manager_common" ]

  external_deps = [
    "ability_base:configuration",
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_single",
    "napi:ace_napi",
    "ffrt:libffrt",
  ]

  if (product_name != "qemu-arm-linux-min") {
    external_deps += [
      "hiappevent:hiappevent_innerapi",
      "time_service:time_client",
      "ability_runtime:wantagent_innerkits",
    ]
  }

  innerapi_tags = [ "platformsdk" ]
  part_name = "netmanager_base"
  subsystem_name = "communication"
}
