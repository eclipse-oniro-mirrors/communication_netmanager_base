# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//foundation/communication/netmanager_base/netmanager_base_config.gni")

rust_cxx("statistics_ani_cxx") {
  sources = [ "src/wrapper.rs" ]
}

ohos_static_library("statistics_ani_static") {
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  include_dirs = [
    "include",
    "${target_gen_dir}/src",
  ]
  sources = [ "src/cxx/statistics_ani.cpp" ]
  sources += get_target_outputs(":statistics_ani_cxx")
  
  deps = [
    ":statistics_ani_cxx",
    "$INNERKITS_ROOT/netstatsclient:net_stats_manager_if",
    "$NETMANAGER_BASE_ROOT/utils:net_manager_common",
  ]
  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_core",
    "napi:ace_napi",
    "rust_cxx:cxx_cppdeps",
  ]

  part_name = "netmanager_base"
  subsystem_name = "communication"
}

ohos_rust_shared_library("statistics_ani") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    boundary_sanitize = true
    all_ubsan = true
    debug = false
  }

  sources = [ "src/lib.rs" ]
  
  external_deps = [
    "runtime_core:ani",
    "hilog:hilog_rust",
    "rust_cxx:lib"
  ]
  deps = [
    ":statistics_ani_static",
    "../../../../common/ani_rs:ani_rs",
    "../../../../common/ani_sys:ani_sys",
    "//third_party/rust/crates/serde/serde:lib"
  ]
  clippy_lints = "none"
  part_name = "netmanager_base"
  subsystem_name = "communication"
  output_extension = "so"
}

generate_static_abc("statistics") {
  base_url = "./ets"

  files = [ 
    "ets/@ohos.net.statistics.d.ets",
  ]

  is_boot_abc = "True"
  device_dst_file = "/system/framework/statistics.abc"
}

ohos_prebuilt_etc("statistics_etc") {
  source = "$target_out_dir/statistics.abc"
  module_install_dir = "framework"
  part_name = "netmanager_base"
  subsystem_name = "communication"
  deps = [ ":statistics" ]
}
